

pipeline WorldEconomyPipeline {
  GdpPerCapitaExtractor
    -> GdpPerCapitaXLSXInterpreter
    -> GdpPerCapitaSheetpicker
    -> DataSelector
    -> NameHeaderWriter;

  NameHeaderWriter
    -> GdpPerCapitaTableInterpreter
    -> GdpPerCapitaLoader;
  
  NameHeaderWriter
    -> BondIssuanceTableInterpreter
    -> BondIssuanceLoader;

  block GdpPerCapitaExtractor oftype HttpExtractor {
    url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
  }

  
  block GdpPerCapitaXLSXInterpreter oftype XLSXInterpreter { }

  block GdpPerCapitaSheetpicker oftype SheetPicker {
    sheetName: 'Figure S5.1.2';
  }

  block DataSelector oftype CellRangeSelector {
   select: range P2:S45;
  }

  block NameHeaderWriter oftype CellWriter {
    at: range A1:D1;
    write: [
      "Country Code",
      "Economy",
      "GDP per Capita",
      "Bond Issuance Share",
    ];
  }

  block GdpPerCapitaTableInterpreter oftype TableInterpreter {
    header: true;
    columns: [
      "Country Code" oftype CountryCodeAlpha3,
      "GDP per Capita" oftype GdpType,
    ];
  }

  block BondIssuanceTableInterpreter oftype TableInterpreter {
    header: true;
    columns: [
      "Country Code" oftype CountryCodeAlpha3,
      "Bond Issuance Share" oftype BondShareType,
    ];
  }


  valuetype GdpType oftype decimal
  {
      constraints: [GdpRange];
  }

  constraint GdpRange on decimal:
      value >= 0;

  valuetype BondShareType oftype decimal
  {
      constraints: [BondShareRange];
  }

  constraint BondShareRange on decimal:
      value >= 0 and value <= 1;
  
  block GdpPerCapitaLoader oftype SQLiteLoader {
    table: "gdpPerCapita";
    file: "./country-stats.sqlite";
  }

  block BondIssuanceLoader oftype SQLiteLoader {
    table: "bondIssuance";
    file: "./country-stats.sqlite";
  }

}
